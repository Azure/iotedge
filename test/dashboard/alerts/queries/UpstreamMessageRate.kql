let environmentPrefix = "<ENVIRONMENT>";
let platform = "<PLATFORM>";
let loadGenMessageRateThreshold = <LOADGEN.THRESHOLD>;
let tempFilterMessageRateThreshold = <TEMPFILTER.THRESHOLD>;
let mostRecentTestId = toscalar(sanitizedTestMetrics 
| where device contains environmentPrefix and device contains platform
| order by TimeGenerated
| take 1
| project testId);
let filtered = sanitizedTestMetrics
| where testId == mostRecentTestId;
let filteredWithMaxTime = filtered
| summarize maxTimeGenerated = max(TimeGenerated);
let maxTimeGenerated = toscalar(filteredWithMaxTime);
let alertContext = filtered
| where TimeGenerated < maxTimeGenerated and TimeGenerated > maxTimeGenerated - <ALERTING.INTERVAL>m;
let MessagesSentToUpstreamAtEndTime = alertContext
| project device, moduleName, messageTarget, Name_s, value, TimeGenerated
| where Name_s == "edgehub_messages_sent_total"
| where messageTarget == "upstream"
| summarize arg_max(TimeGenerated, *) by moduleName
| extend upstreamMessagesEnd = value
| project-away device, messageTarget, TimeGenerated, value, Name_s;
let MessagesSentToUpstreamAtStartTime = alertContext
| project device, moduleName, messageTarget, Name_s, value, TimeGenerated
| where Name_s == "edgehub_messages_sent_total"
| where messageTarget == "upstream"
| summarize arg_min(TimeGenerated, *) by moduleName
| extend upstreamMessagesStart = value
| project-away device, messageTarget, TimeGenerated, value, Name_s;
let MessagesSentToUpstream = MessagesSentToUpstreamAtEndTime
| join kind = inner(MessagesSentToUpstreamAtStartTime) on moduleName
| project-away moduleName1
| extend upstream_message_rate_per_min = (upstreamMessagesEnd  - upstreamMessagesStart) / <ALERTING.INTERVAL>
| project-away upstreamMessagesEnd, upstreamMessagesStart;
MessagesSentToUpstream
| extend distanceFromThreshold = iff(moduleName contains "loadGen", upstream_message_rate_per_min - loadGenMessageRateThreshold, upstream_message_rate_per_min - tempFilterMessageRateThreshold)
| where distanceFromThreshold < 0